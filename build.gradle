plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'nu.studer.jooq' version '7.1.1'
}

group = 'com.dansplugins'
version = '2.0.0-beta.1'

def repoUsername = ""
def repoPassword = ""
def propertiesFile = new File("repo.properties")
if (propertiesFile.exists()) {
    def properties = new Properties()
    properties.load(new FileInputStream(propertiesFile))
    repoUsername = properties.getProperty("username")
    repoPassword = properties.getProperty("password")
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/public/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://repo.mikeprimm.com/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.rpkit.com/repository/maven-public/' }
    maven { url 'https://repo.dansplugins.com/repository/maven-public/' }
}


configurations {
    flywayMigration
}

dependencies {
    implementation 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
    implementation 'com.dansplugins:medieval-factions:5.0.0-beta.7'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    implementation 'com.dansplugins:ponder-bukkit:2.0.0'
    implementation 'me.clip:placeholderapi:2.11.2'
    implementation platform("dev.forkhandles:forkhandles-bom:2.2.0.0")
    implementation 'dev.forkhandles:result4k'
    implementation 'org.jooq:jooq:3.17.4'
    implementation 'org.flywaydb:flyway-core:9.4.0'
    implementation 'org.flywaydb:flyway-mysql:9.4.0'
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.0.6'
    implementation 'com.h2database:h2:2.1.214'
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'org.slf4j:slf4j-api:1.8.0-beta4'
    implementation 'com.dansplugins:ponder-bukkit:2.0.0'
    implementation 'com.dansplugins:ponder-commands:2.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'io.mockk:mockk:1.13.2'
    jooqGenerator 'org.jooq:jooq-meta-extensions:3.17.4'
}

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

import org.jooq.meta.jaxb.Logging

jooq {
    configurations {
        main {
            generationTool {
                logging = Logging.WARN
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.extensions.ddl.DDLDatabase'
                        inputSchema = 'PUBLIC'
                        includes = '.*'
                        excludes = ''
                        properties {
                            property {
                                key = "scripts"
                                value = "src/main/resources/com/dansplugins/currencies/db/migration"
                            }
                            property {
                                key = "sort"
                                value = "semantic"
                            }
                            property {
                                key = "defaultNameCase"
                                value = "lower"
                            }
                        }
                    }
                    target {
                        packageName = 'com.dansplugins.currencies.jooq'
                    }
                }
            }
        }
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filter ReplaceTokens, tokens: [
            'version': version
    ]
}

shadowJar {
    mergeServiceFiles()
    dependencies {
        exclude(dependency("::"))
    }

    relocate 'com.google', 'com.dansplugins.factionsystem.shadow.com.google'
    relocate 'org.mariadb', 'com.dansplugins.factionsystem.shadow.org.mariadb'
    relocate 'com.sun', 'com.dansplugins.factionsystem.shadow.com.sun'
    relocate 'com.zaxxer', 'com.dansplugins.factionsystem.shadow.com.zaxxer'
    relocate 'dev.forkhandles', 'com.dansplugins.factionsystem.shadow.dev.forkhandles'
    relocate 'google', 'com.dansplugins.factionsystem.shadow.google'
    relocate 'io.r2dbc', 'com.dansplugins.factionsystem.shadow.io.r2dbc'
    relocate 'jakarta', 'com.dansplugins.factionsystem.shadow.jakarta'
    relocate 'kotlin', 'com.dansplugins.factionsystem.shadow.kotlin'
    relocate 'org.bstats', 'com.dansplugins.factionsystem.shadow.org.bstats'
    relocate 'org.flywaydb', 'com.dansplugins.factionsystem.shadow.org.flywaydb'
    relocate 'org.h2', 'com.dansplugins.factionsystem.shadow.org.h2'
    relocate 'org.intellij', 'com.dansplugins.factionsystem.shadow.org.intellij'
    relocate 'org.jetbrains', 'com.dansplugins.factionsystem.shadow.org.jetbrains'
    relocate 'org.jooq', 'com.dansplugins.factionsystem.shadow.org.jooq'
    relocate 'org.reactivestreams', 'com.dansplugins.factionsystem.shadow.org.reactivestreams'
    relocate 'preponderous.ponder', 'com.dansplugins.factionsystem.shadow.preponderous.ponder'
}

artifacts {
    archives jar
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            credentials {
                username repoUsername
                password repoPassword
            }
            def releasesRepoUrl = "https://repo.dansplugins.com/repository/maven-releases/"
            def snapshotsRepoUrl = "https://repo.dansplugins.com/repository/maven-snapshots/"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
    publications {
        maven(MavenPublication) { publication ->
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
        }
    }
}
